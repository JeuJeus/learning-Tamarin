theory eq2
begin
/* for this example you should use the command
tamarin-prover  interactive --diff shared/eq2.spthy
in order to execute tamarin
*/
/* penc is probabilistic encryption */

/* lemma 'Secret' states that the plaintext remains secret */
/* lemma  'Observational_equivalence' automatically gerenrated by Tamarin with the option '--diff" states that the plaintext is a strong secret, the adversary cannot distinguish the encryption of the known plaintext from the   encryption of an unknown plaintext */

functions: penc/3, pdec/2, pk/1

equations: pdec(penc(message, pk(key), random), key)=message

//PKI
rule Register_Publickey:
[ Fr(~longtermkey) ]  
--> 
[ 
    !Longtermkey($A,~longtermkey), 
    !Pk($A, pk(~longtermkey)) 
]

rule Out_Publickey:
[ !Pk($A, publickey) ] --> [ Out(publickey) ]

rule Example1:
let 
    publickey = pk(longtermkey)
in
[ 
    !Longtermkey($A, longtermkey), 
    Fr(~fakeMessage), 
    Fr(~realMessage), 
    Fr(~random) 
]
--[ Secret(~realMessage) ]->
[ 
    Out(~fakeMessage), 
    Out(diff(  
        penc(~fakeMessage , publickey, ~random), 
        penc(~realMessage , publickey, ~random) 
    )) 
]

//Properties
lemma Secret:
"
  All B #i. Secret(B)@i ==> not( Ex #j. K(B)@j )
"

end 