theory OAuthInsecureChannel
begin

/* SECURE CHANNELS */

rule SecureChannelSend:
  [ OutSecure($From, $To, m) ]
  -->
  [ !Sec($From, $To, m) ]

rule SecureChannelReceive:
  [ !Sec($From, $To, m) ]
  -->
  [ InSecure($From, $To, m) ]

/* COMPROMISE */
rule CompromisedSend:
  [ 
    OutSecure($From, $To, message),
    !Compromised($From) ]
  -->
  [ 
    !Sec($From,$To,message),
    Out(message)
   ]

rule CompromisedReceive:
  [ 
    !Sec($From, $To, message),
    !Compromised($From)
  ]
  -->
  [ 
    InInsecure($From, $To, message),
    Out(message) 
  ]

rule MaliciousSend:
  [ 
    !Compromised($From),
    In(message)
   ]
  -->
  [ 
    !Sec($From,$To, message)
   ]

rule MaliciousReceive:
   [
    !Compromised($To),
    In(message)
   ]
   -->
   [
    InSecure($From, $To, message)
   ]

rule CompromiseOfEndpoint:
  []
  --[ 
    CompromiseHappened(),
    WasCompromised($Client)
   ]->
  [ !Compromised($Endpoint) ]

/* SETUP OF ENDPOINTS */

rule AuthorizationServer:
  [] --> [ !AuthorizationServer($Server, $AuthenticationEndpoint, $TokenEndpoint)]

rule Client:
  [] --> [ !Client($Client)]

rule RegisterClientAtAuthorizationServer:
  [ 
    !AuthorizationServer($Server, $AuthenticationEndpoint, $TokenEndpoint),
    !Client($Client),
    Fr(~sharedSecret)
  ]
  -->
  [ !Registration($Client, $Server, ~sharedSecret, $RedirectURI)]

/* CORE PROTOCOL */

rule AuthorizationRequest:
let
  message = <'authorization_request', $Client, $Scope, ~state, $RedirectURI>
in
  [
    !AuthorizationServer($Server, $AuthenticationEndpoint, $TokenEndpoint),
    !Registration($Client, $Server, ~sharedSecret, $RedirectURI),
    Fr(~state)
  ] 
  --[
    InitiateAuthorizationFlow($Client, $Server, $Scope),
    SendMessage($Client, message)
  ]-> 
  [
    OutSecure($Client, $AuthenticationEndpoint, message),
    ClientState_AtAuthorizationRequest($Client, $Server, $RedirectURI, ~state)
  ]

rule AuthorizationResponse:
let
    incomingMessage = <'authorization_request', $Client, $Scope, state, $RedirectURI>
    outgoingMessage = <'authorization_response', state, ~authorizationCode>
in
  [
    !AuthorizationServer($Server, $AuthenticationEndpoint, $TokenEndpoint),
    !Registration($Client, $Server, ~sharedSecret, $RedirectURI),
    InSecure($Client, $AuthenticationEndpoint, incomingMessage),
    Fr(~authorizationCode)
  ] 
  --[
    AuthenticMessage($Client, incomingMessage),
    SendMessage($AuthenticationEndpoint, outgoingMessage)
  ]-> 
  [
    OutSecure($AuthenticationEndpoint, $RedirectURI, outgoingMessage),
    AuthorizationServerState_AtAuthorizationResponse($Server, $Client, $RedirectURI, $Scope, ~authorizationCode)
  ]

rule AccessTokenRequest:
let
  incomingMessage = <'authorization_response', ~state, authorizationCode>
  outgoingMessage = <'token_request',$Client, authorizationCode, ~sharedSecret>
in 
[
  !AuthorizationServer($Server, $AuthenticationEndpoint, $TokenEndpoint),
  !Registration($Client, $Server, ~sharedSecret, $RedirectURI),
  ClientState_AtAuthorizationRequest($Client, $Server, $RedirectURI, ~state),
  InSecure($AuthenticationEndpoint, $RedirectURI, incomingMessage)
] 
--[
  AuthenticMessage($AuthenticationEndpoint,incomingMessage),
  SendMessage($Client,outgoingMessage)
]-> 
[
  OutSecure($Client, $TokenEndpoint, outgoingMessage)
]

rule AccessTokenResponse:
let
  incomingMessage = <'token_request',$Client, ~authorizationCode, ~sharedSecret>
  outgoingMessage = <'token_response', ~token>
in
[
  !AuthorizationServer($Server, $AuthenticationEndpoint, $TokenEndpoint),
  !Registration($Client, $Server, ~sharedSecret, $RedirectURI),
  AuthorizationServerState_AtAuthorizationResponse($Server, $Client, $RedirectURI, $Scope, ~authorizationCode),
  InSecure($Client, $TokenEndpoint, incomingMessage),
  Fr(~token)
] 
--[
  AuthenticMessage($Client,incomingMessage),
  SuccessfullyIssuedToken($Client, $Server, $Scope, ~token),
  TokenIsAssumedSecret(~token),
  TokenEndpointIsAssumedUncompromised($TokenEndpoint)
]-> 
[
  OutSecure($TokenEndpoint, $Client, outgoingMessage)
]

/* LEMMAS */

lemma Executability:
  exists-trace
  "Ex client server scope token #i #j . 
    InitiateAuthorizationFlow(client,server,scope)@i &
    SuccessfullyIssuedToken(client,server,scope,token)@j &
    (not (Ex #k . CompromiseHappened()@k))"

lemma SecrecyOfToken:
  "All token #i .
    TokenIsAssumedSecret(token)@i 
      ==> 
        (not Ex #j . K(token)@j) |
        (Ex tokenEndpoint #k #l . 
          TokenEndpointIsAssumedUncompromised(tokenEndpoint)@k & 
          WasCompromised(tokenEndpoint)@l)"

lemma AuthenticMessages:
  "All sender message #j. 
    AuthenticMessage(sender,message)@j 
      ==> 
        Ex #i. 
          SendMessage(sender,message)@i & 
          i<j"

lemma TokenAgreement:
  "All client server scope token #j .
    SuccessfullyIssuedToken(client,server,scope,token)@j
      ==>
        Ex #i . 
            InitiateAuthorizationFlow(client,server,scope)@i & 
            i<j"

end
