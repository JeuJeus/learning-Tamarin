/*
 *  Author: Julius Figge
 *  Model Name: 2x_Experiment_ExtensionSigningWithPresharedKey.spthy
 *  Status: DEVELOPMENTAL
 *
 *  Comments: this does not provide protection against fraudulent ACK injection by Adversary due to dependency on sharedKey as basis, just out of curiosity
 */

theory 2x_Experiment_ExtensionSigningWithPresharedKey
begin

builtins: signing

functions: keyDerivationFunction/3

rule RegisterAgent:
    [ ]-->[ !Agent($X) ]

rule ExternallyPresharedMasterKey:
    [ 
        !Agent($A),
        !Agent($B),
        Fr(~preSharedMasterKey)
     ]
    --[  ]->
    [ 
        !PreSharedMasterKey($A, $B, ~preSharedMasterKey)
     ]

rule A_ComputeAndSendNonce:
    let
        signatureOverNonceFromA = sign(~nonceFromA, preSharedMasterKey)
        messageToB = <$A, ~nonceFromA, signatureOverNonceFromA>
    in
    [ 
        !PreSharedMasterKey($A, $B, preSharedMasterKey),
        Fr(~nonceFromA)
     ]
    --[ StartedSharedKeyProcess($A, $B) ]->
    [ 
        Out(messageToB),
        !State_A_AfterSendingNonce($A, $B, ~nonceFromA)
     ]

rule B_ReceiveNonceComputOwnAndSend:
    let
        messageFromA = <$A, nonceFromA, nonceFromASignature>
        signatureOverNonceFromB = sign(~nonceFromB, preSharedMasterKey)
        messageToA = <$B, ~nonceFromB, signatureOverNonceFromB>  
    in
    [ 
        In(messageFromA),
        !PreSharedMasterKey($A, $B, preSharedMasterKey),
        Fr(~nonceFromB) ]
    --[ 
        Eq(verify(nonceFromASignature,nonceFromA,pk(preSharedMasterKey)), true)
     ]->
    [ 
        Out(messageToA),
        !State_B_AfterReceivingNonce($B, $A, nonceFromA, ~nonceFromB)
     ]

rule A_RecieveNonceInstallSessionKey:
    let
        messageFromB = <$B, nonceFromB, nonceFromBSignature>    
        sharedKey = keyDerivationFunction(preSharedMasterKey, nonceFromA, nonceFromB)
    in
    [ 
        !State_A_AfterSendingNonce($A, $B, nonceFromA),
        !PreSharedMasterKey($A, $B, preSharedMasterKey),
        In(messageFromB) 
    ]
    --[ 
        Eq(verify(nonceFromBSignature,nonceFromB,pk(preSharedMasterKey)), true)
     ]->
    [ 
        !State_A_AfterKeyDerivation($A, $B, sharedKey)
     ]

rule A_SendACK: 
    let
        ackMesage = 'ACK' 
        messageToB = <$A, ackMesage, sign(ackMesage, preSharedMasterKey)>
    in
    [ 
        !PreSharedMasterKey($A, $B, preSharedMasterKey),
        !State_A_AfterKeyDerivation($A, $B, sharedKey) 
    ]
    --[ 
        Done($A),
        SharedKeyGeneratedByA($A, $B, sharedKey)
     ]->
    [ 
        Out(messageToB),
        !Status_A($A,'DONE') 
    ]

rule B_ReceiveACK:
    let
        ackMesage = 'ACK'      
        messageFromA = <$A, ackMesage, ackMesageSignature>
        sharedKey = keyDerivationFunction(preSharedMasterKey, nonceFromA, nonceFromB)
    in
    [ 
        !State_B_AfterReceivingNonce($B, $A, nonceFromA, nonceFromB),
        !PreSharedMasterKey($A, $B, preSharedMasterKey),
        In(messageFromA) 
    ]
    --[ 
        Eq(verify(ackMesageSignature,ackMesage,pk(preSharedMasterKey)), true),
        Done($B),
        SharedKeyGeneratedByB($A, $B, sharedKey)
    ]->
    [ 
        !State_B_AfterACK($B, $A, sharedKey),
        !Status_B($B,'DONE') 
    ]
    

restriction EqualityCheckSuccess: 
    "All x y #i. Eq(x,y) @ i ==> x = y"    

lemma Executable:
    exists-trace
    "All A B #i .
        StartedSharedKeyProcess(A,B)@i 
            ==>
                (Ex #j #k .
                    Done(A)@j &
                    Done(B)@k &
                    i<j & j<k)
    "
    
lemma SharedKeySecrecyAfterBDone:
    "All A B sharedKey #i #j.
        Done(B)@i &
        SharedKeyGeneratedByB(A,B,sharedKey)@j
            ==> 
                (not (Ex #x . K(sharedKey)@x))
    "

lemma SharedKeySecrecyAfterADone:
    "All A B sharedKey #i #j.
        Done(A)@i &
        SharedKeyGeneratedByA(A,B,sharedKey)@j
            ==> 
                (not (Ex #x . K(sharedKey)@x))
    "

lemma BCanNotFinishIfADidNot://With the present constraints this does not hold, the attacker can inject a <A,'ACK'> message w/o A actually generating the key
    "
    All B A sharedKey #j .
        SharedKeyGeneratedByB(A,B,sharedKey)@j
            ==> 
            (Ex #i . 
                SharedKeyGeneratedByA(A,B,sharedKey)@i &
                i < j
            )
    "

end
